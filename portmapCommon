//#  (c) 2009 CodePlex Foundation
//#
//#  Title:                  Port Mapper Program Protocol
//#
//#  Details:                
//#
//#  Public References:      RFC 1057, 1833
//#
//#  Comments:               
//#
//#  Revision Class and Date:Major, 3/3/2009
//#                          Minor, 9/3/2009
//#
//####

Protocol Portmap = FormatString("MessageType = %s, Version = %d", SunRPCMessageType ? "Service Reply" : "Service Call", conversation.ProgramVersion)
{
	switch(SunRPCMessageType)
	{
		case 0: //Service Call
			switch(conversation.ProgramVersion)
			{
				case 2:
					Switch(conversation.ProcedureNumber)
					{
						case 0:
						case 4:
							_struct{};
						case 1:
						case 2:
						case 3:
							_struct Mapping
							{
								UINT32 Program;
								UINT32 Version;
								UINT32 Protocol = ProtocolTypeTable(this);
								UINT32 Port;
							}
						case 5:
							_struct CallIt
							{
								UINT32 Program;
								UINT32 Version;
								UINT32 Protocol = ProtocolTypeTable(this);
								XDRVarLenOpaqueData OpaqueData;
							}
					}
			}
		case 1: //Service Reply
			switch(conversation.ProgramVersion)
			{
				case 2:
					Switch(conversation.ProcedureNumber)
					{
						case 0:
							_struct{};
						case 1:
						case 2:
							XDRBoolean ReturnValue;
						case 3:
							[post.global.RpcMappedPort$[MappedPort] = true]
							UINT32 MappedPort;
						case 4:
							_struct PMappingList
							{
								
								while[UINT32(FrameData, FrameOffSet)]
								{
									XDRBoolean Pointer;
									struct Mapping
									{
										UINT32 Program;
										UINT32 Version;
										UINT32 Protocol = ProtocolTypeTable(this);
										UINT32 Port;
									};
								}
								XDRBoolean PointerEnd;
							}
						case 5:
							_struct CallItResult
							{
								UINT32 Port;
								XDRVarLenOpaqueData OpaqueData;
							}
					}
			}
	}
}