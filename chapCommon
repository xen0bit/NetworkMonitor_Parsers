//#  (c) 2009 CodePlex Foundation
//#
//#  Title:                  Challenge Handshake Authentication Protocol
//#
//#  Details:                
//#
//#  Public References:      RFC 1994
//#
//#  Comments:               
//#
//#  Revision Class and Date:
//#
//####

Protocol CHAP = FormatString("%s, ID = %d, Length = %d %s",CHAPCodeTable(Code), Identifier, Length, ChapSummary)
{
	UINT8 Code = FormatString("%s, %d(0x%X)", CHAPCodeTable(this), this, this);
	UINT8 Identifier;
	[CHAPEndOffset = offset + this - 2, ChapSummary = ""]
	UINT16 Length;
	switch(Code)
	{
		case 1:
		case 2:
			_struct ChallengeOrResponse
			{
				UINT8 ValueSize;
				switch
				{
					case ValueSize > 0: AsciiString(ValueSize) Value;
				}
				switch
				{
					case offset < Property.CHAPEndOffset:
						[ChapSummary = FormatString(", Name = '%s'", this)]
						AsciiString(Property.CHAPEndOffset - offset) Name;
				}
			};
		case 3:
		case 4:
			switch
			{
			  	case Length > 4:
			  		[ChapSummary = FormatString(", Message = '%s'", this)]
					AsciiString(Length - 4) Message;	
			};
		default:
			switch
			{
			  	case Length > 4: AsciiString(Length - 4) Data;
			};
	}
}
Table CHAPCodeTable(value)
{
	switch(value)
	{
		case  1: "Challenge";
		case  2: "Response";
		case  3: "Success";
		case  4: "Failure";
		default: "Undefined code";
	}
}


