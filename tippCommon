//#  (c) 2009 CodePlex Foundation
//#
//#  Title:                  Transaction Internet Protocol (TIP) Extensions
//#
//#  Details:                
//#
//#  Public References:      RFC 2371
//#  Microsoft References:   [MS-TIPP]: Transaction Internet Protocol (TIP) Extensions (5.0)
//#
//#  Comments:               
//#
//#  Revision Class and Date:Major, 9/16/2009
//#
//####

Protocol TIPP = FormatString("commandLine:%s ", Summary)
{
	while wond[FrameOffset < FrameLength]
	{
		[Summary = Summary + " " + commandLine]
		AsciiStringTerm( "\n") commandLine
		{
			switch(AsciiStringTerm( FrameData, FrameOffset, " "))
			{
				// request
				case "IDENTIFY":
					Struct
					{
						AsciiStringTerm(" ") IDENTIFYCommand;
						AsciiStringTerm(" ") LowestProtocolVersion;
						AsciiStringTerm(" ") HighestProtocolVersion;
						AsciiStringTerm(" ") PrimaryTransactionManagerAddress;
						AsciiStringTerm("\n") SecondaryTransactionManagerAddress;
					}
				case "MULTIPLEX":
					Struct
					{
						AsciiStringTerm(" ") MULTIPLEXCommand;
						AsciiStringTerm ("\n") ProtocolIdentifier;
					}
				case "PULL":
					Struct
					{
						AsciiStringTerm(" ") PULLCommand;
						AsciiStringTerm(" ") SuperiorTransactionIdentifier;
						AsciiStringTerm("\n") SubordinateTransactionIdentifier;
					}
				case "PUSH":
					Struct
					{
						AsciiStringTerm(" ") PUSHCommand;
						AsciiStringTerm ("\n") SuperiorTransactionIdentifier;
					}
				case "QUERY":
					Struct
					{
						AsciiStringTerm(" ") QUERYCommand;
						AsciiStringTerm ("\n") SuperiorTransactionIdentifier;
					}
				case "RECONNECT":
					Struct
					{
						AsciiStringTerm(" ") RECONNECTCommand;
						AsciiStringTerm ("\n") SubordinateTransactionIdentifier;
					}
				// response
				case "BEGUN":
					Struct
					{
						AsciiStringTerm(" ") BEGUNCommand;
						AsciiStringTerm ("\n") TransactionIdentifier;
					}
				case "IDENTIFIED":
					Struct
					{
						AsciiStringTerm(" ") IDENTIFIEDCommand;
						AsciiStringTerm ("\n") ProtocolVersion;
					}
				case "PUSHED":
					Struct
					{
						AsciiStringTerm(" ") PUSHEDCommand;
						AsciiStringTerm ("\n") SubordinateTransactionIdentifier;
					}
				case "ALREADYPUSHED":
					Struct
					{
						AsciiStringTerm(" ") ALREADYPUSHEDCommand;
						AsciiStringTerm ("\n") SubordinateTransactionIdentifier;
					}
				default:
					Switch(AsciiStringTerm( FrameData, FrameOffset, "\n"))
					{
						// request
						case "ABORT":
						case "BEGIN":
						case "COMMIT":
						case "ERROR":
						case "PREPARE":
						case "TLS":
						// response
						case "ABORTED":
						case "NOTBEGUN":
						case "COMMITTED":
						case "NEEDTLS":
						case "MULTIPLEXING":
						case "CANTMULTIPLEX":
						case "PREPARED":
						case "READONLY":
						case "PULLED":
						case "NOTPULLED":
						case "NOTPUSHED":
						case "QUERIEDEXISTS":
						case "QUERIEDNOTFOUND":
						case "RECONNECTED":
						case "NOTRECONNECTED":
						case "TLSING":
						case "CANTTLS":
							AsciiStringTerm("\n") TIPCommand;
						default:
							Struct
							{
								ReportParserError( ParserErrorProtocolClassWindows, "TIPP", "Unknown Data" ) UnknownData;
								AsciiStringTerm("\n") UnknownCommand;
							}
					}
			}
		};
	}
}

